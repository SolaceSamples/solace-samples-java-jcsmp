apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

// Don't need these task, so disabling them. Makes it possible to avoid
// declaring a single application main class.
startScripts.enabled = false
run.enabled = false

// Also don't need the regular application distribution packages since
// this is just a set of samples. So disabling to make the build output
// cleaner
distTar.enabled=false
distZip.enabled=false

applicationName = 'solace-samples-java'
version = ''

jar {
    baseName = 'solace-samples-java'
    version =  version
    manifest {
        attributes 'Implementation-Title': 'Solace Getting Started Samples',
                   'Implementation-Version': version
    }
}

// Download context sensitive help and/or source code for eclipse and idea
eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Solace Messaging API for Java Dependencies
    compile("com.solacesystems:sol-jcsmp:10.5.0")
}

// unused, so commenting out (Issue #58)
//task createAllStartScripts() << {
     // just a placeholder
//}

  def scripts = ['topicPublisher':'com.solace.samples.TopicPublisher',
				 'topicSubscriber':'com.solace.samples.TopicSubscriber',
				 'queueProducer':'com.solace.samples.QueueProducer',
				 'queueConsumer':'com.solace.samples.QueueConsumer',
				 'basicRequestor':'com.solace.samples.BasicRequestor',
				 'basicReplier':'com.solace.samples.BasicReplier',
				 'confirmedPublish':'com.solace.samples.ConfirmedPublish',
				 'topicToQueueMapping':'com.solace.samples.TopicToQueueMapping',
                 'featureMessageSelectorsOnQueue':'com.solace.samples.features.MessageSelectorsOnQueue',
                 'featureMessageTTLAndDeadMessageQueue':'com.solace.samples.features.MessageTTLAndDeadMessageQueue',
                 'featureQueueProvisionAndBrowse':'com.solace.samples.features.QueueProvisionAndBrowse',
                 'featureQueueProvisionAndRequestActiveFlowIndication':'com.solace.samples.features.QueueProvisionAndRequestActiveFlowIndication',
                 'featureTransactions':'com.solace.samples.features.Transactions',
                 'featureMessageReplay':'com.solace.samples.features.MessageReplay'
]
  scripts.each() { scriptName, className ->
          def t = tasks.create(name: scriptName+'StartScript', type: CreateStartScripts) {
        mainClassName = className
        applicationName = scriptName
        outputDir = new File(project.buildDir, 'scripts')
        classpath = jar.outputs.files + project.configurations.runtime
    }
      applicationDistribution.into("bin") {
            from(t)
            fileMode = 0755
			duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
    //createAllStartScripts.dependsOn(t)
}

installDist {
    destinationDir = new File(project.buildDir, 'staged')
}

assemble.dependsOn installDist

allprojects {
    tasks.withType(Test).configureEach {
        maxParallelForks = 4
        if (!project.hasProperty("createReports")) {
            reports.html.required = false
            reports.junitXml.required = false
        }
    }

}
